//  gradle publishMainReleasePublicationToMavenRepository
plugins {
    id 'java-library'
    id("maven-publish")

    //    id 'signing'
}

// Android编译器只支持到JDK17
compileJava {
    //  会覆盖sourceCompatibility
    options.release = 17
}

sourceSets {
    javaSecond {
        java {
            srcDirs = ['src/main/javaSecond']
        }
    }
}

compileJavaSecondJava {
    options.release = 18    //  UTF-8、jwebserver
}

jar {
    manifest {
        attributes("Multi-Release": true)
        attributes("Implementation-Title": "Open-Java",
                   "Implementation-Version": archiveVersion)
    }
    into('META-INF/versions/18') {
        from sourceSets.javaSecond.output
    }
}

test {
    useJUnitPlatform()
}

dependencies {    
    javaSecondImplementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }

    //  测试api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'    
    //  测试引擎
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
}

afterEvaluate { project ->
    project.tasks.all { Task task ->
        if (task.name ==~ /publish.*PublicationToMavenRepository/) {
            task.doFirst {
                /*
                println "Set task's network proxy"
                System.setProperty("http.proxyHost", "127.0.0.1")
                System.setProperty("http.proxyPort", "10800")
                System.setProperty("https.proxyHost", "127.0.0.1")
                System.setProperty("https.proxyPort", "10800")
                 */
            }
        }
    }
}
